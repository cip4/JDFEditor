import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'org.beryx.runtime' version '1.12.7'
}

mainClassName = 'org.cip4.tools.jdfeditor.Editor'
def simpleProjectName = "JDFEditor"

sourceCompatibility = 17
targetCompatibility = 17

if (project.hasProperty('projectVersion')) {
version = project.projectVersion
} else {
    version = 'development'
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
	}
}

processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: [
		'name'     : project.properties['name'],
		'version'  : project.properties['version'],
		'timestamp': new Date().format('yyyy-MM-dd HH:mm:ss')
        ])
	}
}

application {
    mainClass = 'org.cip4.tools.jdfeditor.Editor'
}

configurations {
    all*.exclude group: 'xml-apis', module: 'xml-apis'
}

dependencies {
    implementation 'org.cip4.lib.jdf:JDFLibJ-JSON:+'
    implementation 'org.cip4.tools.jdfutility:JDFUtility:1.8.+'
	implementation ('org.cip4.lib.jdf:JDFLibJ:2.1.8.+'){
		exclude group: 'xml-apis'
	}
	implementation ('xerces:xercesImpl:2.12.2'){transitive=false}
    implementation 'org.eclipse.jetty:jetty-servlet:10.0.7'
    implementation ('com.googlecode.json-simple:json-simple:1.1.1'){transitive=false}
    implementation 'commons-configuration:commons-configuration:1.10'
    implementation 'commons-collections:commons-collections:3.2.2'
    implementation 'com.sun.woodstock.dependlibs:jhall:2.0'
    implementation 'com.fifesoft:rsyntaxtextarea:3.5.0'
	
    implementation 'commons-logging:commons-logging-api:1.1'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'commons-io:commons-io:2.16.1'

    implementation 'javax.mail:mail:1.4.7'

	
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.+'
}

runtime {
    modules = [
	'jdk.xml.dom',
	'jdk.unsupported'
    ]
    additive = true
    jpackage {
        imageOptions = [
		"--copyright", "CIP4 Organization",
		"--vendor", "CIP4 Organization"
        ]
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def icon = "src/main/dist/windows/jdfeditor_128.ico"
        if (currentOs.macOsX) {
            imageOptions += [
			"--mac-sign",
			"--mac-signing-key-user-name", "CIP4 Organization (4DL5XX9SG8)",
			"--mac-package-identifier", "org.cip4.jdfeditor"
            ]
            icon = "src/main/dist/macosx/jdfeditor.icns"
		}
        if (currentOs.windows) {
            installerOptions += [
			"--win-dir-chooser",
			"--win-menu",
			"--win-upgrade-uuid", "800cfb27-5ece-4275-9ed3-1a305b5b1636",
			"--vendor", "CIP4 Organization"
            ]
		}
        imageOptions += ["--icon", icon]
        appVersion = project.version == "development" ? "1.0.0" : project.version
	}
}

java {
    archivesBaseName = simpleProjectName
}

startScripts {
    applicationName = simpleProjectName
}

test {
    dependsOn(jre)
    executable = jre.getJreDir().dir("bin").file("java").getAsFile().getAbsolutePath()
}
