import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id "edu.sc.seis.launch4j" version "2.4.9"
    id "edu.sc.seis.macAppBundle" version "2.3.0"
}

sourceCompatibility = 11
targetCompatibility = 11

if (project.hasProperty('projectVersion')) {
    version = project.projectVersion
} else {
    version = 'development'
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'org.cip4.tools.jdfeditor.Editor'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens:  [
                'name': project.properties['name'],
                'version': project.properties['version'],
                'timestamp': new Date().format('yyyy-MM-dd HH:mm:ss')
        ])
    }
}

application {
    mainClass = 'org.cip4.tools.jdfeditor.Editor'
}

createExe {
    mainClassName = 'org.cip4.tools.jdfeditor.Editor'
    icon = "${projectDir}/src/main/dist/windows/jdfeditor_128.ico"
    outfile = project.name + '-' + project.version + '.exe'
}

macAppBundle {
    mainClassName = 'org.cip4.tools.jdfeditor.Editor'
    icon = "${projectDir}/src/main/dist/macosx/jdfeditor.icns"
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
}

dependencies {
    compile 'org.cip4.lib.jdf:JDFLibJ:2.1.7.x-SNAPSHOT'
    compile 'org.apache.logging.log4j:log4j-api:2.0-rc1'
    compile 'org.apache.logging.log4j:log4j-core:2.0-rc1'
    compile 'commons-configuration:commons-configuration:1.10'
    compile 'commons-collections:commons-collections:3.2.1'
    compile 'com.sun.woodstock.dependlibs:jhall:2.0'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.24.v20191120'
    compile 'org.eclipse.jetty:jetty-server:9.4.24.v20191120'
    compile 'org.eclipse.jetty:jetty-webapp:9.4.24.v20191120'
    compile 'com.fifesoft:rsyntaxtextarea:1.4.1'

    testCompile 'junit:junit:4.13'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}
