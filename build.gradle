import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.5.1'
    }
}

plugins {
    id 'java'
    id 'application'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

sourceCompatibility = 11
targetCompatibility = 11

if (project.hasProperty('projectVersion')) {
    version = project.projectVersion
} else {
    version = 'development'
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'org.cip4.tools.jdfeditor.Editor'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens:  [
                'name': project.properties['name'],
                'version': project.properties['version'],
                'timestamp': new Date().format('yyyy-MM-dd HH:mm:ss')
        ])
    }
}

application {
    mainClass = 'org.cip4.tools.jdfeditor.Editor'
}

configurations {
   all*.exclude group: 'xml-apis', module: 'xml-apis'
 }

dependencies {
    compile 'org.cip4.lib.jdf:JDFLibJ:2.1.7.+'
    compile 'org.cip4.lib.jdf:JDFLibJ-JSON:+'
    compile 'org.apache.logging.log4j:log4j-api:2.0-rc1'
    compile 'org.apache.logging.log4j:log4j-core:2.0-rc1'
    compile 'commons-configuration:commons-configuration:1.10'
    compile 'commons-collections:commons-collections:3.2.1'
    compile 'com.sun.woodstock.dependlibs:jhall:2.0'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.24.v20191120'
    compile 'org.eclipse.jetty:jetty-server:9.4.24.v20191120'
    compile 'org.eclipse.jetty:jetty-webapp:9.4.24.v20191120'
    compile 'com.fifesoft:rsyntaxtextarea:1.4.1'
    compile 'com.googlecode.json-simple:json-simple:1.1'

    testCompile 'junit:junit:4.13'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}

task packageApp(type: io.github.fvarrui.javapackager.gradle.PackageTask, dependsOn: build) {
    appName = "${project.name}"
    mainClass = 'org.cip4.tools.jdfeditor.Editor'
    bundleJre = true
    customizedJre = false // TODO: Fix the problem with the customized Jre and re-enable it.
    generateInstaller = false
    administratorRequired = false
    platform = "mac"
    winConfig {
        icoFile = file("${projectDir}/src/main/dist/windows/jdfeditor_128.ico")
        generateSetup = false
        generateMsi = false
        generateMsm = false
    }
    macConfig {
        icnsFile = file("${projectDir}/src/main/dist/macosx/jdfeditor.icns")
        generateDmg = true
        generatePkg = false
        developerId = "Developer ID Application: CIP4 Organization (4DL5XX9SG8)"
        entitlements = file("entitlements.plist")
    }
}